plugins {
    id 'war'
    id 'com.github.node-gradle.node' version '2.2.1'
    id 'io.miret.etienne.sass' version '1.1.0'
}

group 'io.miret.etienne.hourglass'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.2.4.RELEASE')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.2.4'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

node {
    version = '13.8.0'
    yarnVersion = '1.22.0'
    download = true
}

yarn_install {
    outputs.dir "${projectDir}/node_modules"
}

task webpack (type: YarnTask) {
    args = ['webpack', '-o', "${buildDir}/webpack/bundle.js"]
    inputs.dir "${projectDir}/src/main/ts"
    inputs.file "${projectDir}/yarn.lock"
    inputs.file "${projectDir}/tsconfig.json"
    inputs.file "${projectDir}/webpack.config.js"
    outputs.dir "${buildDir}/webpack"
    dependsOn yarn_install
}

task jest (type: YarnTask) {
    def report = "${buildDir}/test-results/jest/result.json"
    args = ['jest', "--outputFile=${report}"]
    inputs.dir "${projectDir}/src/main/ts"
    inputs.dir "${projectDir}/src/test/ts"
    inputs.file "${projectDir}/yarn.lock"
    inputs.file "${projectDir}/tsconfig.json"
    outputs.file report
    dependsOn yarn_install
}

war {
    with copySpec {
        from "${buildDir}/webpack"
    }
    with copySpec {
        from "${buildDir}/sass"
    }
    dependsOn webpack
}

test {
    useJUnitPlatform ()
    dependsOn jest
}

clean {
    delete 'node_modules'
}
