plugins {
    id 'war'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.github.node-gradle.node' version '2.2.0'
    id 'com.github.salomonbrys.gradle.sass' version '1.2.0'
}

group 'io.miret.etienne.hourglass'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-dependencies:2.2.0.RELEASE'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.1.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

node {
    version = '13.0.1'
    yarnVersion = '1.19.1'
    download = true
}

sass {
    download {
        version = '1.23.3'
    }
}

task yarn_install {
    outputs.dir "${projectDir}/node_modules"
}

task webpack (type: YarnTask) {
    args = ['webpack', '-o', "${buildDir}/webpack/bundle.js"]
    inputs.dir "${projectDir}/src/main/ts"
    inputs.file "${projectDir}/yarn.lock"
    inputs.file "${projectDir}/tsconfig.json"
    inputs.file "${projectDir}/webpack.config.js"
    outputs.dir "${buildDir}/webpack"
    dependsOn yarn_install
}

task jest (type: YarnTask) {
    def report = "${buildDir}/test-results/jest/result.json"
    args = ['jest', "--outputFile=${report}"]
    inputs.dir "${projectDir}/src/main/ts"
    inputs.dir "${projectDir}/src/test/ts"
    inputs.file "${projectDir}/yarn.lock"
    inputs.file "${projectDir}/tsconfig.json"
    outputs.file report
    dependsOn yarn_install
}

war {
    with copySpec {
        from "${buildDir}/webpack"
    }
    with copySpec {
        from "${buildDir}/sass"
    }
    dependsOn (webpack, sassCompile)
}

test {
    useJUnitPlatform ()
    dependsOn jest
}

clean {
    delete 'node_modules'
}
